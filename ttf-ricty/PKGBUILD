# Maintainer : Moriya Kazumi <kuroclef@gmail.com>

pkgname=ttf-ricty
pkgver=latest
pkgrel=1
pkgdesc='Fonts for programming.'
arch=(any)
url=https://github.com/kuroclef/Ricty
license=(custom)
makedepends=(fontforge git ttf-inconsolata)
_miguver=20200307
source=(git+https://github.com/kuroclef/Ricty.git
        https://osdn.dl.osdn.net/mix-mplus-ipa/72511/migu-1m-"$_miguver".zip)
md5sums=(SKIP aaf550f0b79ab8d2a902acb9f675b256)

pkgver() {
  cd Ricty
  printf %s "$(./ricty_generator.sh -V | head -1 | cut -d\  -f3)"
}

build() {
  cd Ricty
  mv ../migu-1m-"$_miguver"/*.ttf .
  ./ricty_generator.sh auto
}

package() {
  cd Ricty
  install -d                  "$pkgdir"/usr/share/fonts/TTF
  install -m  644 Ricty-*.ttf "$pkgdir"/usr/share/fonts/TTF
  install -Dm 644 README.md   "$pkgdir"/usr/share/doc/"$pkgname"/README.md
}
