# Maintainer : Kazumi Moriya <kuroclef@gmail.com>

pkgname=ibus-mozc-neologd-ut
pkgver=2.23.2815.102.20191125
pkgrel=1
pkgdesc='A Japanese Input Method Editor designed for multi-platform.'
arch=(x86_64)
url=http://linuxplayers.g1.xrea.com/mozc-neologd-ut.html
license=(custom)
depends=(ibus qt5-base)
makedepends=(clang fcitx gtk2 ninja python2 zinnia)
provides=("${pkgname#ibus-}")
conflicts=("${pkgname#ibus-}")
source=(http://dl.osdn.jp/users/24/24949/mozc-neologd-ut-2.23.2815.102.20191125.1.tar.xz
        add-new-japanese-era.patch
        fix-for-gcc81.patch)
md5sums=(5bab38e5c2c438376b5eb96a49b22992
         bf171fb519b59917f46222f832912599
         8433e14c819d2f41e33f4c5e4412515a)

prepare() {
  cd "${pkgname#ibus-}"-"$pkgver"."$pkgrel"/src
  patch -Np2 -i "$srcdir"/add-new-japanese-era.patch
  patch -Np2 -i "$srcdir"/fix-for-gcc81.patch
  find . -name  '*.py'       -type f -exec sed -i '1s/python/python2/'     {} \;
  find . -regex '.*\.gypi?$' -type f -exec sed -i "s/'python'/'python2'/g" {} \;
}

build() {
  cd "${pkgname#ibus-}"-"$pkgver"."$pkgrel"/src
  GYP_DEFINES=document_dir=/usr/share/licenses/"${pkgname#ibus-}" python2 build_mozc.py gyp --target_platform=Linux
  python2 build_mozc.py build -c Release unix/ibus/ibus.gyp:ibus_mozc server/server.gyp:mozc_server gui/gui.gyp:mozc_tool renderer/renderer.gyp:mozc_renderer
  sed -i 's|/usr/libexec/|/usr/lib/ibus-mozc/|g' out_linux/Release/gen/unix/ibus/mozc.xml
}

package() {
  cd "${pkgname#ibus-}"-"$pkgver"."$pkgrel"/src
  mkdir -p "$pkgdir"/usr/share/licenses/"${pkgname#ibus-}"
  mv       ../LICENSE data/installer/*.html "$pkgdir"/usr/share/licenses/"${pkgname#ibus-}"

  install -Dm 644 data/images/unix/ime_product_icon_opensource-32.png "$pkgdir"/usr/share/ibus-mozc/product_icon.png
  install -Dm 644 data/images/unix/ui-alpha_full.png       "$pkgdir"/usr/share/ibus-mozc/alpha_full.png
  install -Dm 644 data/images/unix/ui-alpha_half.png       "$pkgdir"/usr/share/ibus-mozc/alpha_half.png
  install -Dm 644 data/images/unix/ui-dictionary.png       "$pkgdir"/usr/share/ibus-mozc/dictionary.png
  install -Dm 644 data/images/unix/ui-direct.png           "$pkgdir"/usr/share/ibus-mozc/direct.png
  install -Dm 644 data/images/unix/ui-hiragana.png         "$pkgdir"/usr/share/ibus-mozc/hiragana.png
  install -Dm 644 data/images/unix/ui-katakana_full.png    "$pkgdir"/usr/share/ibus-mozc/katakana_full.png
  install -Dm 644 data/images/unix/ui-katakana_half.png    "$pkgdir"/usr/share/ibus-mozc/katakana_half.png
  install -Dm 644 data/images/unix/ui-properties.png       "$pkgdir"/usr/share/ibus-mozc/properties.png
  install -Dm 644 data/images/unix/ui-tool.png             "$pkgdir"/usr/share/ibus-mozc/tool.png

  install -Dm 644 out_linux/Release/gen/unix/ibus/mozc.xml "$pkgdir"/usr/share/ibus/component/mozc.xml
  install -Dm 755 out_linux/Release/ibus_mozc              "$pkgdir"/usr/lib/ibus-mozc/ibus-engine-mozc
  install -Dm 755 out_linux/Release/mozc_renderer          "$pkgdir"/usr/lib/mozc/mozc_renderer
  install -Dm 755 out_linux/Release/mozc_server            "$pkgdir"/usr/lib/mozc/mozc_server
  install -Dm 755 out_linux/Release/mozc_tool              "$pkgdir"/usr/lib/mozc/mozc_tool
}
