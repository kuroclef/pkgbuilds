# Maintainer : Moriya Kazumi <kuroclef@gmail.com>

pkgname=ibus-mozc-neologd-ut
_pkgver=2.23.2815.102
_date=20200315
pkgver="$_pkgver"."$_date"
pkgrel=1
pkgdesc='A Japanese Input Method Editor designed for multi-platform.'
arch=(x86_64)
url=http://linuxplayers.g1.xrea.com/mozc-neologd-ut.html
license=(custom)
depends=(ibus protobuf qt5-base)
makedepends=(clang fcitx gtk2 gyp python zinnia)
provides=("${pkgname#ibus-}")
conflicts=("${pkgname#ibus-}")
source=(http://deb.debian.org/debian/pool/main/m/mozc/mozc_"$_pkgver"+dfsg.orig.tar.xz
        http://dl.osdn.jp/users/25/25485/mozcdic-neologd-ut-20200315.1.tar.bz2
        add-new-japanese-era.patch
        fix-for-gcc81.patch
        mozc-2.23.2815.102-python-3.patch)
md5sums=(f0556ea6060afd5f0edfce2c208d53dc
         4d0e18328edd6d556d24581a0cef6196
         bf171fb519b59917f46222f832912599
         8433e14c819d2f41e33f4c5e4412515a
         57458407fb750a0c63f967efb04b6118)

prepare() {
  cd mozc-"$_pkgver"+dfsg/src
  patch -Np2 -i "$srcdir"/add-new-japanese-era.patch
  patch -Np2 -i "$srcdir"/fix-for-gcc81.patch
  patch -Np2 -i "$srcdir"/mozc-2.23.2815.102-python-3.patch
  cat "$srcdir"/mozcdic-neologd-ut-"$_date"."$pkgrel"/mozcdic-*-ut-*.txt >> data/dictionary_oss/dictionary00.txt
  ln -srnf "$srcdir"/../third_party .
}

build() {
  cd mozc-"$_pkgver"+dfsg/src
  GYP_DEFINES='document_dir=/usr/share/licenses/mozc use_libprotobuf=1 use_libzinnia=1' python build_mozc.py gyp --gypdir=/usr/bin --target_platform=Linux
  python build_mozc.py build -c Release unix/ibus/ibus.gyp:ibus_mozc server/server.gyp:mozc_server gui/gui.gyp:mozc_tool renderer/renderer.gyp:mozc_renderer
  sed -i 's|/usr/libexec/|/usr/lib/ibus-mozc/|g' out_linux/Release/gen/unix/ibus/mozc.xml
}

package() {
  cd mozc-"$_pkgver"+dfsg/src
  mkdir -p "$pkgdir"/usr/share/licenses/"${pkgname#ibus-}"
  mv       ../LICENSE data/installer/*.html "$pkgdir"/usr/share/licenses/"${pkgname#ibus-}"

  install -Dm 644 data/images/unix/ime_product_icon_opensource-32.png "$pkgdir"/usr/share/ibus-mozc/product_icon.png
  install -Dm 644 data/images/unix/ui-alpha_full.png       "$pkgdir"/usr/share/ibus-mozc/alpha_full.png
  install -Dm 644 data/images/unix/ui-alpha_half.png       "$pkgdir"/usr/share/ibus-mozc/alpha_half.png
  install -Dm 644 data/images/unix/ui-dictionary.png       "$pkgdir"/usr/share/ibus-mozc/dictionary.png
  install -Dm 644 data/images/unix/ui-direct.png           "$pkgdir"/usr/share/ibus-mozc/direct.png
  install -Dm 644 data/images/unix/ui-hiragana.png         "$pkgdir"/usr/share/ibus-mozc/hiragana.png
  install -Dm 644 data/images/unix/ui-katakana_full.png    "$pkgdir"/usr/share/ibus-mozc/katakana_full.png
  install -Dm 644 data/images/unix/ui-katakana_half.png    "$pkgdir"/usr/share/ibus-mozc/katakana_half.png
  install -Dm 644 data/images/unix/ui-properties.png       "$pkgdir"/usr/share/ibus-mozc/properties.png
  install -Dm 644 data/images/unix/ui-tool.png             "$pkgdir"/usr/share/ibus-mozc/tool.png

  install -Dm 644 out_linux/Release/gen/unix/ibus/mozc.xml "$pkgdir"/usr/share/ibus/component/mozc.xml
  install -Dm 755 out_linux/Release/ibus_mozc              "$pkgdir"/usr/lib/ibus-mozc/ibus-engine-mozc
  install -Dm 755 out_linux/Release/mozc_renderer          "$pkgdir"/usr/lib/mozc/mozc_renderer
  install -Dm 755 out_linux/Release/mozc_server            "$pkgdir"/usr/lib/mozc/mozc_server
  install -Dm 755 out_linux/Release/mozc_tool              "$pkgdir"/usr/lib/mozc/mozc_tool
}
