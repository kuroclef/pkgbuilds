# Maintainer : Moriya Kazumi <kuroclef@gmail.com>

pkgname=uim-mozc-neologd-ut
pkgver=2.23.2815.102.20191125
pkgrel=1
pkgdesc='A Japanese Input Method Editor designed for multi-platform.'
arch=(x86_64)
url=http://linuxplayers.g1.xrea.com/mozc-neologd-ut.html
license=(custom)
depends=(qt5-base uim)
makedepends=(clang fcitx gtk2 ninja python2 zinnia)
provides=("${pkgname#uim-}")
conflicts=("${pkgname#uim-}")
source=(http://dl.osdn.jp/users/24/24949/mozc-neologd-ut-2.23.2815.102.20191125.1.tar.xz
        git://github.com/e-kato/macuim.git
        add-new-japanese-era.patch
        fix-for-gcc81.patch)
md5sums=(5bab38e5c2c438376b5eb96a49b22992
         SKIP
         bf171fb519b59917f46222f832912599
         8433e14c819d2f41e33f4c5e4412515a)

prepare() {
  cd "${pkgname#uim-}"-"$pkgver"."$pkgrel"/src
  patch -Np2 -i "$srcdir"/add-new-japanese-era.patch
  patch -Np2 -i "$srcdir"/fix-for-gcc81.patch
  find . -name  '*.py'       -type f -exec sed -i '1s/python/python2/'     {} \;
  find . -regex '.*\.gypi?$' -type f -exec sed -i "s/'python'/'python2'/g" {} \;
  mv "$srcdir"/macuim/Mozc/uim unix
}

build() {
  cd "${pkgname#uim-}"-"$pkgver"."$pkgrel"/src
  GYP_DEFINES=document_dir=/usr/share/licenses/"${pkgname#uim-}" python2 build_mozc.py gyp --target_platform=Linux
  python2 build_mozc.py build -c Release unix/uim/uim.gyp:uim-mozc server/server.gyp:mozc_server gui/gui.gyp:mozc_tool
}

package() {
  cd "${pkgname#uim-}"-"$pkgver"."$pkgrel"/src
  mkdir -p "$pkgdir"/usr/share/licenses/"${pkgname#uim-}"
  mkdir -p "$pkgdir"/usr/share/uim
  mv       ../LICENSE data/installer/*.html "$pkgdir"/usr/share/licenses/"${pkgname#uim-}"
  cp    -r "$srcdir"/../{pixmaps,scm/*}     "$pkgdir"/usr/share/uim

  install -Dm 755 out_linux/Release/mozc_server    "$pkgdir"/usr/lib/mozc/mozc_server
  install -Dm 755 out_linux/Release/mozc_tool      "$pkgdir"/usr/lib/mozc/mozc_tool
  install -Dm 755 out_linux/Release/libuim-mozc.so "$pkgdir"/usr/lib/uim/plugin/libuim-mozc.so
}
