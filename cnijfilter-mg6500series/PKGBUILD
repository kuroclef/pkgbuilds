# Maintainer : Kazumi Moriya <kuroclef@gmail.com>

pkgname=cnijfilter-mg6500series
pkgver=4.00
pkgrel=1
pkgdesc='PIXUS MG6530 IJ Printer Driver for Linux.'
arch=(i686 x86_64)
url=http://cweb.canon.jp/drv-upd/ij-mfp/linux-pd-mg6530-"${pkgver/./}".html
license=(custom)
depends=(cups ghostscript nss-mdns)
source=(http://gdlp01.c-wss.com/gds/9/0100005509/01/"$pkgname"-"$pkgver"-"$pkgrel"-rpm.tar.gz)
md5sums=(7719f2af53dc32131bd49c09dc15827f)

prepare() {
  cd "$pkgname"-"$pkgver"-"$pkgrel"-rpm/packages
  bsdtar -xf cnijfilter-common-"$pkgver"-"$pkgrel"."${CARCH/i6/i3}".rpm
  bsdtar -xf "$pkgname"-"$pkgver"-"$pkgrel"."${CARCH/i6/i3}".rpm

  cp    -rf usr/lib64/* usr/lib
  rm    -r  usr/{lib64,share/doc/*common*}
  mkdir -p  usr/share/licenses/"$pkgname"
  mv    -t  usr/share/licenses/"$pkgname" usr/share/doc/*/LICENSE*

  for file in usr/lib/lib*; do
    ln -sr "$file" "${file/so.*/so}"
  done
}

package() {
  cd "$pkgname"-"$pkgver"-"$pkgrel"-rpm/packages
  cp -r etc usr "$pkgdir"
}
