# Maintainer : Kazumi Moriya <kuroclef@gmail.com>

pkgname=mfc6490cn
pkgver=1.1.2
pkgrel=2
pkgdesc='LPR and CUPS driver for the Brother MFC-6490CN.'
arch=(x86_64)
url=http://support.brother.co.jp/j/b/downloadlist.aspx?prod=mfc6490cn&os=127
license=(custom)
depends=(cups ghostscript nss-mdns lib32-glibc)
source=(http://download.brother.com/welcome/dlf100654/mfc6490cnlpr-1.1.2-2.i386.rpm
        http://download.brother.com/welcome/dlf100659/mfc6490cncupswrapper-1.1.2-2.i386.rpm)
md5sums=(26f56bd44499295608ae60341f042336
         b7b9cac24b4ce0280e7b4d05bbaa45d2)

prepare() {
  mkdir -p "$srcdir"/usr/lib/cups/filter
  mkdir -p "$srcdir"/usr/share/cups/model
  mv       "$srcdir"/usr/local/Brother "$srcdir"/usr/share
  rm    -r "$srcdir"/usr/local

  grep -lr '/local' "$srcdir"/usr/share | xargs -l sed -i 's|/local|/share|g'

  cd "$srcdir"/usr/share/Brother/Printer/mfc6490cn/cupswrapper
  sed -i -f - cupswrappermfc6490cn <<EOF
/^#.*restart|sleep/i systemctl restart org.cups.cupsd.service
/lpadmin/d
s|/usr|$srcdir/usr|g
s/lpinfo/echo/g
EOF
  ./cupswrappermfc6490cn
  sed -i "s|$srcdir||" "$srcdir"/usr/lib/cups/filter/*lpdwrapper*
  rm cupswrappermfc6490cn
  rm "$srcdir"/usr/share/Brother/Printer/mfc6490cn/inf/setupPrintcapij
}

package() {
  cp -r "$srcdir"/usr "$pkgdir"
}
